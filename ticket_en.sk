options:
	token: your_token #Token
	bot: NameOfyourBot #Name of your bot
	adminrole: Administrator #You must write the exact name of the Administrator role for your server. Example: Administrator.
	name: Server #Server name
	idchannel: 718242310080561212 #Define a room ID to which players should click on the reaction.
	idcategory: 593495192003149838 #Define a category ID in order to store tickets in this category.
	idroleadmin: 539936065079738382 #If you wish to be mentioned when a ticket is created
	idstaffRole: 539938803972964366 #Add the ID of the grade to which you want to give access (example: Enter the ID of the moderator role so that your moderators can have access to the tickets).
	website: https://hydeos.fr #Define the link for your site.
	
function updategame(t: text):
	if {ticket} > 1:
		mark "{@bot}" as playing "View %{ticket}% tickets"
	else:
		mark "{@bot}" as playing "View %{ticket}% ticket"

on load:
	if {ticket} is not set:
		set {ticket} to 0
	login to "{@token}" with name "{@bot}"
	updategame("update")
	
discord command tickets:
	prefixes: !
	bots: {@bot}
	executable in: guild
	trigger:
		purge event-message with "{@bot}"
		if event-member doesn't have role named "{@adminrole}":
			reply with ":x: You don't have permission :x:" and store it in {_reply}
			wait 5 seconds
			purge {_reply} with "{@bot}"
		else:
			make embed:
				set title of embed to title with text "**Ticket**"
				set the description of embed to "Welcome to Support **{@Name}** ! %nl%%nl% Please click on the **reaction** :white_check_mark: to open a new ticket! %nl%%nl% Our team will respond to your **request** as quickly as possible."
				set color of embed to green
			send the last created embed to event-channel with event-bot and store it in {_embed}
			add reaction "white_check_mark" to reactions of {_embed}
			
on reaction add:
	if event-channel's discord id is "{@idchannel}":
		discord name of event-emote = "white_check_mark":
			remove event-emote added by event-user from event-message
			loop text channels of event-guild:
				topic of loop-channel contains "%event-member%":
					reply with ":x: Error: %mention tag of event-member% You already have a ticket :x:" and store it in {_reply}
					wait 5 seconds
					purge {_reply} with "{@bot}"
					stop
			create text channel:
				set the name of the channel to "Ticket-%event-member%"
				set the parent of the channel to category with id "{@idcategory}"
				set topic of channel to "%event-member%"
				create the channel in event-guild and store it in {_ticket}
				add 1 to {ticket}
				updategame("update")
				send "%mention tag of event-member% %mention tag of role with id ""{@idroleadmin}""%" to channel with name "%{_ticket}%" with event-bot
				create embed:
					set the author info of the embed to author named "{@Name}" with url "{@website}" and icon "%avatar of event-bot%"
					set description of embed to "Welcome to Support **{@Name}** ! %nl%%nl% Please detail your request in order to speed up the process of your ticket. %nl%%nl% To close the ticket, please click on the **reaction** :x: %nl% Our team will respond to your **request** as quickly as possible."
					set the color of embed to dark blue
				send the last created embed to channel with name "%{_ticket}%" with event-bot and store it in {_embed}
				add reaction "x" to reactions of {_embed}
				set {_ticketID} to discord id of {_ticket}
				allow event-member permissions (message history, view channel, message read, message write, message embed links and message attach files) in channel with id "%{_ticketID}%" with event-bot
				allow role named "{@idstaffRole}" permissions (message history, view channel, message read, message write, message embed links and message attach files) in channel with id "%{_ticketID}%" with event-bot
				deny public role of event-guild permission view channel in channel with id "%{_ticketID}%" with event-bot
	discord name of event-emote = "x":
		if discord name of event-channel contains "Ticket-":
			remove 1 from {ticket}
			updategame("update")
			delete discord entity event-channel	